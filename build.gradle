plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
}

group = 'com.ordwen'
version="3.0.2"

def ciVersion = System.getenv("ORG_GRADLE_PROJECT_version")
if (ciVersion != null && !ciVersion.trim().isEmpty()) {
    version = ciVersion.trim()
    println "Using CI-injected version: ${version}"
} else {
    println "Using local default version: ${version}"
}

repositories {
    mavenCentral()
}

dependencies {
    /* database management */
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'org.xerial:sqlite-jdbc:3.49.1.0'
    compileOnly 'com.h2database:h2:2.1.214'

    /* Spigot */
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'

    /* Folia */
    implementation 'space.arim.morepaperlib:morepaperlib:0.4.3'

    /* lib for antiglitch */
    compileOnly 'com.jeff-media:custom-block-data:2.2.3'

    /* Mojang auth API for heads */
    compileOnly 'com.mojang:authlib:1.5.25'

    /* economy & points */
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'org.black_ixx:playerpoints:3.2.2'
    compileOnly 'su.nightexpress.coinsengine:CoinsEngine:2.4.1'
    compileOnly('com.github.Realizedd:TokenManager:3.2.4') { transitive = false }

    /* npcs */
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') { exclude group: '*', module: '*' }
    compileOnly 'de.oliver:FancyNpcs:2.7.0'

    /* mobs */
    compileOnly 'io.lumine:Mythic-Dist:5.6.1'
    compileOnly 'com.magmaguy:EliteMobs:9.1.11'

    /* stackers */
    compileOnly 'dev.rosewood:rosestacker:1.5.33'
    compileOnly 'com.bgsoftware:WildStackerAPI:3.8.0'

    /* guis */
    compileOnly 'me.clip:placeholderapi:2.11.6'

    /* custom items */
    compileOnly 'io.th0rgal:oraxen:1.194.1'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14'
    compileOnly 'com.nexomc:nexo:1.1.0'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.2-SNAPSHOT'
    compileOnly 'io.lumine:MythicLib-dist:1.5.2-SNAPSHOT'
    compileOnly 'net.momirealms:custom-crops:3.6.29'
    compileOnly 'net.momirealms:custom-fishing:2.3.4'
    compileOnly('me.kryniowesegryderiusz:kgenerators-core:7.3') { exclude group: '*', module: '*' }

    /* auto restart */
    compileOnly 'com.github.N0RSKA:UltimateAutoRestartAPI:2023.12'

    /* protection */
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.3.1'

    /* vote */
    compileOnly 'com.github.NuVotifier:NuVotifier:2.7.2'

    /* crates */
    compileOnly 'su.nightexpress.excellentcrates:ExcellentCrates:6.0.0'

    /* compatibility libs */
    compileOnly 'com.willfp:eco:6.76.2'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.shadowJar {
    archiveClassifier.set("")
    archiveFileName.set("${project.name}-${project.version}.jar")
    relocate 'io.papermc.lib', 'com.ordwen.odailyquests.lib.paperlib'
    relocate 'space.arim.morepaperlib', 'com.ordwen.odailyquests.lib.morepaperlib'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}

publishing {
    publications {
        shadow(MavenPublication) {
            from(components.shadow)
            version = project.version.replace("-local", "")
        }
    }
}